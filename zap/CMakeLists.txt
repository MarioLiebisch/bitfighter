#------------------------------------------------------------------------------
# Copyright Chris Eykamp
# See LICENSE.txt for full copyright information
#------------------------------------------------------------------------------

# Define the Linux data dir if not defined in a packaging build script already
if("${CMAKE_SYSTEM}" MATCHES "Linux")
	if(NOT LINUX_DATA_DIR)
		set(LINUX_DATA_DIR "/usr/share")
	endif()

	message(STATUS "LINUX_DATA_DIR: ${LINUX_DATA_DIR}.  Change this by invoking cmake with -DLINUX_DATA_DIR=<SOME_DIRECTORY>")

	# Quotes need to be a part of the definition or the compiler won't understand
	add_definitions(-DLINUX_DATA_DIR="${LINUX_DATA_DIR}")
endif()


set(SHARED_SOURCES
	BanList.cpp
	barrier.cpp
	BfObject.cpp
	BotNavMeshZone.cpp
	ChatCheck.cpp
	ClientInfo.cpp
	Color.cpp
	config.cpp
	Console.cpp
	controlObjectConnection.cpp
	CoreGame.cpp
	CTFGame.cpp
	dataConnection.cpp
	EngineeredItem.cpp
	EventManager.cpp
	flagItem.cpp
	game.cpp
	gameConnection.cpp
	gameLoader.cpp
	gameNetInterface.cpp
	GameSettings.cpp
	gameStats.cpp
	gameType.cpp
	gameWeapons.cpp
	Geometry.cpp
	GeomObject.cpp
	GeomUtils.cpp
	goalZone.cpp
	gridDB.cpp
	HTFGame.cpp
	HttpRequest.cpp
	IniFile.cpp
	InputCode.cpp
	item.cpp
	LevelDatabase.cpp
	LevelSource.cpp
	LineItem.cpp
	LoadoutTracker.cpp
	loadoutZone.cpp
	LuaBase.cpp
	LuaGlobals.cpp
	luaGameInfo.cpp
	luaLevelGenerator.cpp
	LuaScriptRunner.cpp
	masterConnection.cpp
	MathUtils.cpp
	md5wrapper.cpp
	move.cpp
	moveObject.cpp
	NexusGame.cpp
	PickupItem.cpp
	playerInfo.cpp
	Point.cpp
	PointObject.cpp
	polygon.cpp
	projectile.cpp
	rabbitGame.cpp
	Rect.cpp
	retrieveGame.cpp
	robot.cpp
	ScreenInfo.cpp
	ServerGame.cpp
	Settings.cpp
	ship.cpp
	shipItems.cpp
	SimpleLine.cpp
	SlipZone.cpp
	soccerGame.cpp
	SoundEffect.cpp
	SoundSystem.cpp
	Spawn.cpp
	speedZone.cpp
	statistics.cpp
	stringUtils.cpp
	SystemFunctions.cpp
	teamInfo.cpp
	teleporter.cpp
	textItem.cpp
	Timer.cpp
	WallSegmentManager.cpp
	WeaponInfo.cpp
	Zone.cpp
	zoneControlGame.cpp
	../clipper/clipper.cpp
	../master/database.cpp
	../master/masterInterface.cpp
	../recast/RecastAlloc.cpp
	../recast/RecastMesh.cpp
	../sqlite/sqlite3.c
	../poly2tri/common/shapes.cc
	../poly2tri/sweep/advancing_front.cc
	../poly2tri/sweep/cdt.cc
	../poly2tri/sweep/sweep.cc
	../poly2tri/sweep/sweep_context.cc
)

set(CLIENT_SOURCES 
	AToBScroller.cpp
	ChatCommands.cpp
	ChatHelper.cpp
	ClientGame.cpp
	Cursor.cpp
	EditorAttributeMenuItemBuilder.cpp
	EditorPlugin.cpp
	EditorTeam.cpp
	EnergyGaugeRenderer.cpp
	engineerHelper.cpp
	Event.cpp
	FontManager.cpp
	FpsRenderer.cpp
	gameObjectRender.cpp
	HelpItemManager.cpp
	HelperManager.cpp
	helperMenu.cpp
	Joystick.cpp
	JoystickRender.cpp
	LevelDatabaseDownloadThread.cpp
	LevelDatabaseRateThread.cpp
	LevelDatabaseUploadThread.cpp
	lineEditor.cpp
	LoadoutIndicator.cpp
	loadoutHelper.cpp
	oglconsole.cpp
	OpenglUtils.cpp
	quickChatHelper.cpp
	RenderUtils.cpp
	ScissorsManager.cpp
	ScreenShooter.cpp
	ShipShape.cpp
	SlideOutWidget.cpp
	sparkManager.cpp
	SymbolShape.cpp
	TeamShuffleHelper.cpp
	TimeLeftRenderer.cpp
	UI.cpp
	UIAbstractInstructions.cpp
	UIChat.cpp
	UICredits.cpp
	UIDiagnostics.cpp
	UIEditor.cpp
	UIEditorInstructions.cpp
	UIEditorMenus.cpp
	UIErrorMessage.cpp
	UIGame.cpp
	UIGameParameters.cpp
	UIHighScores.cpp
	UIInstructions.cpp
	UIKeyDefMenu.cpp
	UILevelInfoDisplayer.cpp
	UIManager.cpp
	UIMenuItems.cpp
	UIMenus.cpp
	UIMessage.cpp
	UINameEntry.cpp
	UIQueryServers.cpp
	UITeamDefMenu.cpp
	VideoSystem.cpp
	voiceCodec.cpp
	../fontstash/stb_truetype.c
	../fontstash/fontstash.c
)

file(GLOB BITFIGHTER_HEADERS *.h)

file(GLOB OTHER_HEADERS
	../other/*.h 
	../fontstash/*.h 
	../clipper/*.h* 
	../recast/*.h 
	../sqlite/*.h
	../poly2tri/*.h
	../poly2tri/common/*.h
	../poly2tri/sweep/*.h
)

if(WIN32 AND NOT XCOMPILE)
	# Add icon resource in Visual Studio
	list(APPEND CLIENT_SOURCES ZAP.rc)
endif()

if(APPLE)
	list(APPEND SHARED_SOURCES Directory.mm)
endif()


# Library stuff
if(WIN32)
	set(EXTRA_LIBS ws2_32 winmm)
else()
	set(EXTRA_LIBS dl m)
endif()

# libraries used by both client and server
set(SHARED_LIBS
	${CMAKE_THREAD_LIBS_INIT}
	lua-vec
	tnl
	tomcrypt
	${EXTRA_LIBS}
)

# libraries used only on the client
set(CLIENT_LIBS
	${SDL_LIBRARY}
	${PNG_LIBRARY}
	${OPENGL_LIBRARY}
	${OPENAL_LIBRARY}
	${ALURE_LIBRARIES}
	${MODPLUG_LIBRARIES}
	${OGG_LIBRARIES}
	${SPEEX_LIBRARIES}
	${VORBIS_LIBRARIES}
	${VORBISFILE_LIBRARIES}
)

if(APPLE)
	list(APPEND CLIENT_LIBS ${SPARKLE_LIBRARIES})
endif()


##
## Full client build
##
if(SDL_FOUND OR SDL2_FOUND)
	add_executable(bitfighter
		${SHARED_SOURCES}
		${CLIENT_SOURCES}
		${BITFIGHTER_HEADERS}
		${OTHER_HEADERS}
		main.cpp
	)

	add_dependencies(bitfighter
		alure
		lua-vec
		tnl
		tomcrypt
	)

	target_link_libraries(bitfighter
		${CLIENT_LIBS}
		${SHARED_LIBS}
	)

	include_directories(
		${OPENAL_INCLUDE_DIR}
		${OPENGL_INCLUDE_DIR}
		${PNG_INCLUDE_DIR}
		${SDL_INCLUDE_DIR}
		${SPEEX_INCLUDE_DIR}
		${SPARKLE_INCLUDE_DIR}
	)

	# Where to put the executable
	set_target_properties(bitfighter PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/exe)
	
	if(USE_GLES AND SDL2_FOUND)
		message(STATUS "Forcing usage of OpenGL ES")

		get_property(CLIENT_DEFS TARGET bitfighter PROPERTY COMPILE_DEFINITIONS)
		set_target_properties(bitfighter
			PROPERTIES
			COMPILE_DEFINITIONS "${CLIENT_DEFS};BF_USE_GLES"
		)
	endif()
endif()


##
## Dedicated server build
##
add_executable(bitfighterd
	EXCLUDE_FROM_ALL
	${SHARED_SOURCES}
	main.cpp
)

add_dependencies(bitfighterd
	tnl
	lua-vec
	tomcrypt
)

target_link_libraries(bitfighterd
	${SHARED_LIBS}
)

set_target_properties(bitfighterd
	PROPERTIES
	RUNTIME_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/exe
)

get_property(DEDICATED_DEFS TARGET bitfighterd PROPERTY COMPILE_DEFINITIONS)
set_target_properties(bitfighterd
	PROPERTIES
	COMPILE_DEFINITIONS "${DEDICATED_DEFS};ZAP_DEDICATED"
)

include_directories(
	${CMAKE_SOURCE_DIR}/tnl
	${CMAKE_SOURCE_DIR}/boost
)


##
## Test runner executable
##
option(BITFIGHTER_COVERAGE "Add coverage information to the test executable and create 'coverage' target" NO)

set(TEST_SOURCES
	${CMAKE_SOURCE_DIR}/bitfighter_test/main_test.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestGeomUtils.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestHttpRequest.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestLevelLoader.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestLuaEnvironment.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestPolylineGeometry.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestObjects.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestRobot.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestServerGame.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestSpawnDelay.cpp
	${CMAKE_SOURCE_DIR}/bitfighter_test/TestUtils.cpp
	${CMAKE_SOURCE_DIR}/master/master.cpp
	${CMAKE_SOURCE_DIR}/master/MasterServerConnection.cpp
)

add_executable(test EXCLUDE_FROM_ALL
	${CLIENT_SOURCES}
	${SHARED_SOURCES}
	${TEST_SOURCES}
	${BITFIGHTER_HEADERS}
)

target_link_libraries(test
	${SHARED_LIBS}
	${CLIENT_LIBS}
	gtest
)

add_dependencies(test
	alure
	lua-vec
	tnl
	tomcrypt
	gtest
)

if(NOT MSVC)
	add_definitions(-iquote ${CMAKE_SOURCE_DIR}/zap)
endif()

set_target_properties(test
	PROPERTIES
	RUNTIME_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/exe
	COMPILE_DEFINITIONS BITFIGHTER_TEST
)

include_directories(
	${CMAKE_SOURCE_DIR}/gtest/include
	${CMAKE_SOURCE_DIR}/zap
)

# to use the coverage target, install lcov, enable BITFIGHTER_COVERAGE, and run it
# coverage data is output to the 'cov' directory in html format.
if(BITFIGHTER_COVERAGE)
   set_target_properties(test
      PROPERTIES
      LINK_FLAGS "--coverage"
      COMPILE_FLAGS "--coverage"
   )

   add_custom_target(coverage cd ${CMAKE_SOURCE_DIR}/exe && ${CMAKE_SOURCE_DIR}/exe/test
      COMMAND lcov --capture --directory ${CMAKE_SOURCE_DIR} --output-file ${CMAKE_SOURCE_DIR}/build/coverage.info
      COMMAND lcov --extract ${CMAKE_SOURCE_DIR}/build/coverage.info --output-file ${CMAKE_SOURCE_DIR}/build/coverage.info ${CMAKE_SOURCE_DIR}/zap/*
      COMMAND genhtml ${CMAKE_SOURCE_DIR}/build/coverage.info --output-directory ${CMAKE_SOURCE_DIR}/build/cov
      DEPENDS test alure lua-vec tnl tomcrypt
      WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/build 
   )
endif()


##
## Settings for all targets
##

# You can only call 'set_target_properties' once for each properties-target combination, therefore
# we'll build up a list and call 'set_target_properties' at the end
set(ALL_DEBUG_DEFS
	"TNL_DEBUG"
)

if(WIN32)
	# Install windows libraries and resources
	file(TO_NATIVE_PATH ${CMAKE_SOURCE_DIR}/resource/ resDir)
	file(TO_NATIVE_PATH ${CMAKE_SOURCE_DIR}/lib/ libDir)
	file(TO_NATIVE_PATH ${CMAKE_SOURCE_DIR}/exe exeDir)
	
	if(MSYS OR CYGWIN OR XCOMPILE)
		set(RES_COPY_CMD cp -r ${resDir}* ${exeDir})
		set(LIB_COPY_CMD cp ${libDir}*.dll ${exeDir})
	else()
		set(RES_COPY_CMD xcopy /e /d /y ${resDir}* ${exeDir})
		set(LIB_COPY_CMD xcopy /d /y ${libDir}*.dll ${exeDir})
	endif()
	
	add_custom_command(TARGET test bitfighterd bitfighter POST_BUILD 
		COMMAND ${RES_COPY_CMD}
		COMMAND ${LIB_COPY_CMD}
	)
endif()

# Visual Studio only settings
if(MSVC)
	# Work around the "Debug", "Release", etc. directories Visual Studio tries to add
	foreach(OUTPUTCONFIG ${CMAKE_CONFIGURATION_TYPES})
		string(TOUPPER ${OUTPUTCONFIG} OUTPUTCONFIG)
		set_target_properties(test bitfighterd bitfighter 
			PROPERTIES RUNTIME_OUTPUT_DIRECTORY_${OUTPUTCONFIG} ${CMAKE_SOURCE_DIR}/exe
		)
	endforeach()

	# Set some linker flags to use console mode in debug build, etc..
	# Always use SUBSYSTEM:CONSOLE; hiding the console window is controlled in zap/main.cpp near the bottom of main()
	# Allows console to stay visible if ran from typing in command window.
	set_target_properties(test bitfighterd bitfighter PROPERTIES LINK_FLAGS_DEBUG "/SUBSYSTEM:CONSOLE")
	set_target_properties(test bitfighterd bitfighter PROPERTIES LINK_FLAGS_RELWITHDEBINFO "/SUBSYSTEM:CONSOLE")
	set_target_properties(test bitfighterd bitfighter PROPERTIES LINK_FLAGS_RELEASE "/SUBSYSTEM:CONSOLE")
	set_target_properties(test bitfighterd bitfighter PROPERTIES LINK_FLAGS_MINSIZEREL "/SUBSYSTEM:CONSOLE")
	
	# Set more compiler flags for console on appropriate targets
	list(APPEND ALL_DEBUG_DEFS "_CONSOLE")
endif()

set_target_properties(test bitfighterd bitfighter PROPERTIES COMPILE_DEFINITIONS_DEBUG "${ALL_DEBUG_DEFS}")
